apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "com.example.android.architecture.blueprints.todoapp"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables{
            useSupportLibrary = true
        }

        multiDexEnabled = true
        multiDexKeepProguard file('multi-dex-keep.txt')
    }

    buildTypes {
        debug {
            minifyEnabled true
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    flavorDimensions "default"

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
        }
        prod {
            dimension "default"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

/*
 Dependency versions are defined in the top level build.gradle file. This helps keeping track of
 all versions in a single place. This improves readability and helps managing project complexity.
 */
dependencies {
    // App's dependencies, including test
    implementation 'androidx.preference:preference:1.1.0-rc01'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha09'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.2.0'
    implementation ("com.google.guava:guava:$rootProject.guavaVersion") {
        exclude group: "org.checkerframework", module: "checker-compat-qual"
        exclude group: "com.google.errorprone", module: "error_prone_annotations"
        exclude group: "com.google.guava", module: "failureaccess"
    }
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    annotationProcessor "androidx.room:room-compiler:$rootProject.roomVersion"

    // AndroidX Test Orchestrator
    androidTestUtil "androidx.test:orchestrator:$rootProject.androidXTestVersion"

    // AndroidX Test runner and rules
    androidTestImplementation "androidx.test:runner:$rootProject.runnerVersionAndroidX"
    androidTestImplementation "androidx.test:rules:$rootProject.rulesVersionAndroidX"
    androidTestImplementation "androidx.room:room-testing:$rootProject.roomVersion"

    // Espresso UI Testing
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersionAndroidX"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.espressoVersionAndroidX"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$rootProject.espressoVersionAndroidX"
    androidTestImplementation "androidx.test.espresso:espresso-web:$rootProject.espressoVersionAndroidX"
    androidTestImplementation "androidx.test.espresso:espresso-accessibility:$rootProject.espressoVersionAndroidX"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$rootProject.espressoVersionAndroidX"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.jUnitVersion"
    androidTestImplementation "androidx.test:core:$rootProject.androidXTestVersion"
    androidTestImplementation "androidx.test.ext:truth:$rootProject.androidXTestVersion"

    // Accessibility and UI Automator dependencies
    androidTestImplementation "com.google.android.apps.common.testing.accessibility.framework:accessibility-test-framework:$rootProject.accessibilityFramework"
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    // Squareup dependencies
    androidTestImplementation ("com.squareup.okhttp3:okhttp:3.12.0") {
        force = true
    }
    implementation "com.squareup.picasso:picasso:2.5.2"

    // ConditionalWatcher dependency
    androidTestImplementation "com.azimolabs.conditionwatcher:conditionwatcher:0.2"
}
